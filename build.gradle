import org.apache.tools.ant.filters.*

apply plugin: 'java'
apply plugin: "kotlin"
apply plugin: 'maven'
apply plugin: "com.github.johnrengelman.shadow"

group = 'me.elsiff'
version = '2.2.0-SNAPSHOT'

ext.pluginName = 'MoreFish'
ext.author = 'elsiff'
ext.website = 'https://elsiff.me'
ext.mainPackage = 'me.elsiff.morefish'
ext.mainClass = mainPackage + ".MoreFish"

sourceCompatibility = 1.8
targetCompatibility = 1.8

buildscript {
    ext.kotlin_version = '1.3.11'

    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "com.github.jengelman.gradle.plugins:shadow:4.0.3"
    }
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

repositories {
    jcenter()
    mavenCentral()
    maven { url "https://hub.spigotmc.org/nexus/content/repositories/snapshots/" }
    maven { url "http://nexus.hc.to/content/repositories/pub_releases" }
    maven { url "http://repo.citizensnpcs.co/" }
    maven { url "http://repo.extendedclip.com/content/repositories/placeholderapi/" }
    maven { url "http://repo.dmulloy2.net/nexus/repository/public/" }
    maven { url "http://maven.sk89q.com/repo/" }
    maven { url = "https://repo.aikar.co/content/groups/aikar/" }
    maven { url = "https://hub.spigotmc.org/nexus/content/groups/public/" }
}

compileJava {
    options.compilerArgs += ["-parameters"]
    options.fork = true
    options.forkOptions.executable = 'javac'
}
compileKotlin {
    kotlinOptions.javaParameters = true
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.1.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.1.0'
    testImplementation "com.nhaarman.mockitokotlin2:mockito-kotlin:2.0.0"

    compile "co.aikar:acf-core:0.5.0-SNAPSHOT"
    compile "co.aikar:acf-paper:0.5.0-SNAPSHOT"
    compileOnly group: 'com.comphenix.protocol', name: 'ProtocolLib-API', version: '4.2.1'
    compileOnly group: 'com.sk89q', name: 'worldguard', version: '6.1'
    compileOnly group: 'org.spigotmc', name: 'spigot', version: '1.13-R0.1-SNAPSHOT'
    compileOnly group: 'net.milkbowl.vault', name: 'VaultAPI', version: '1.6'
    compileOnly group: 'net.citizensnpcs', name: 'citizensapi', version: '2.0.18-SNAPSHOT'
    compileOnly group: 'me.clip', name: 'placeholderapi', version: '2.7.1'
    compileOnly files('libs/mcMMO.jar')
    testCompile "org.jetbrains.kotlin:kotlin-test:$kotlin_version"
    testCompile "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
    implementation 'com.beust:klaxon:5.0.1'
}

shadowJar {
    relocate 'co.aikar.commands', "${mainPackage}.acf"
}
build { dependsOn shadowJar }

processResources {
    filter ReplaceTokens, tokens: [
            "pluginName"     : pluginName,
            "project.version": project.property("version"),
            "author"         : author,
            "website"        : website,
            "mainClass"      : mainClass
    ]
}